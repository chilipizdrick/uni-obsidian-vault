struct Uniforms {
    model: mat4x4f,
    view_proj: mat4x4f,
    normal: mat3x3f,
    camera_pos: vec3f,
}

@group(0) @binding(0) var<uniform> uniforms: Uniforms;

struct VertexInput {
    @location(0) pos: vec3f,
    @location(1) normal: vec3f,
};

struct VertexOutput {
    @builtin(position) clip_pos: vec4f,
    @location(0) world_pos: vec3f,
    @location(1) normal: vec3f,
}

@vertex
fn vs_main(vertex: VertexInput) -> VertexOutput {
    let local_pos = vec4f(vertex.pos, 1.0);
    var out: VertexOutput;

    let world_pos = uniforms.model * local_pos;

    out.world_pos = world_pos.xyz;
    out.clip_pos = uniforms.view_proj * world_pos;

    out.normal = uniforms.normal * normalize(vertex.normal);

    return out;
}

const light_dir: vec3f = vec3f(-1.0, -1.0, 1.0);
const ambient_color: vec3f = vec3f(0.2);
const diffuse_color: vec3f = vec3f(0.7);
const specular_color: vec3f = vec3f(0.7);
const frag_color: vec4f = vec4f(vec3f(0.75), 1.0);

@fragment
fn fs_main(in: VertexOutput) -> @location(0) vec4f {
    let normal = normalize(in.normal);
    let light_dir = normalize(light_dir);

    let diffuse = max(dot(normal, light_dir), 0.0);

    let camera_dir = normalize(uniforms.camera_pos - in.world_pos);
    let half_dir = normalize(camera_dir + light_dir);

    let specular = pow(max(dot(normal, half_dir), 0.0), 8.0);

    let lighting_color = ambient_color + (diffuse * diffuse_color) + (specular * specular_color);

    return frag_color * vec4f(lighting_color, 1.0);
}
